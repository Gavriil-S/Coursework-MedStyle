Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр ИсторияЗаписей
	Движения.ИсторияЗаписей.Записывать = Истина;
	Движение = Движения.ИсторияЗаписей.Добавить();
	Движение.Период = Дата;
	Движение.МедицинскийЦентр = МедицинскийЦентр;
	Движение.Клиент = Клиент;
	Движение.Сотрудник = Сотрудник;
	Движение.Услуга = Услуга;
	Движение.Сумма = Сумма;
	
	МенеджерЗаписи = РегистрыСведений.ОчередьОтправкиУведомлений.СоздатьМенеджерЗаписи(); 
	МенеджерЗаписи.Документ = Ссылка;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Документ.Пустая() Тогда
		МенеджерЗаписи.Документ = Ссылка;
		МенеджерЗаписи.ДатаПриема = Дата;
		МенеджерЗаписи.Статус = Статус;
		МенеджерЗаписи.Отправлено = Ложь;
	Иначе
		Если МенеджерЗаписи.ДатаПриема <> Дата Тогда
			МенеджерЗаписи.ДатаИзменилась = Истина;	
		КонецЕсли; 
		Если МенеджерЗаписи.Статус <> Статус Тогда
			МенеджерЗаписи.СтатусИзменился = Истина;
		КонецЕсли; 
		МенеджерЗаписи.ДатаПриема = Дата;
		МенеджерЗаписи.Статус = Статус;
		МенеджерЗаписи.ОписаниеОшибки = "";
		МенеджерЗаписи.Отправлено = Ложь;
	КонецЕсли;
	МенеджерЗаписи.Записать(Истина);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	ДатаОкончания = Дата + Длительность * 60;
	УстановитьЦветПоСтатусу();
	
	СтруктураДанныхЗаписи = Новый Структура;
	СтруктураДанныхЗаписи.Вставить("Дата", Дата);
	СтруктураДанныхЗаписи.Вставить("ДатаОкончания", ДатаОкончания);
	СтруктураДанныхЗаписи.Вставить("Сотрудник", Сотрудник);
	СтруктураДанныхЗаписи.Вставить("ТекущаяЗапись", Ссылка);
	ЗаписьВозможна = Документы.Запись.ЗаписьНаВыделенноеВремяВозможна(СтруктураДанныхЗаписи); 
	
	Если НЕ ЗаписьВозможна Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю("Запись на данное время невозможна!");
	КонецЕсли;
КонецПроцедуры



Процедура УстановитьЦветПоСтатусу()
	Если ЭтоНовый() Тогда
		Статус = Перечисления.СтатусЗаявки.Новая;	
	КонецЕсли;
	Если Статус = Перечисления.СтатусЗаявки.Новая Тогда
		Цвет = Новый ХранилищеЗначения(WebЦвета.ЗеленаяЛужайка);
	ИначеЕсли Статус = Перечисления.СтатусЗаявки.Завершено Тогда	
		Цвет = Новый ХранилищеЗначения(WebЦвета.БледноБирюзовый);
	ИначеЕсли Статус = Перечисления.СтатусЗаявки.ВОбработке Тогда	
		Цвет = Новый ХранилищеЗначения(WebЦвета.БледноЗолотистый);
	ИначеЕсли Статус = Перечисления.СтатусЗаявки.Отменено Тогда	
		Цвет = Новый ХранилищеЗначения(WebЦвета.Красный);
	КонецЕсли;
КонецПроцедуры


